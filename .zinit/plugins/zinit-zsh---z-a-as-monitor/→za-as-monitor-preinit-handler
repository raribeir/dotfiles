#!/usr/bin/env zsh
# -*- mode: sh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-

# Copyright (c) 2019 Sebastian Gniazdowski

# Set the base and typically useful options
builtin emulate -LR zsh
builtin setopt extendedglob warncreateglobal typesetsilent noshortloops rcquotes

[[ ${ZINIT_ICE[as]} != (monitor*|*monitor) && ${ZINIT_ICE[.monitor]} != (monitor*|*monitor) ]] && return 0
[[ $1 = plugin ]] && {
    builtin print -P -- "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-error]}ERROR: as'${ZINIT_ICE[as]}'" \
        "ice can be used only with snippets%f"
    return 0
}

local __type="$1" __url="$2" __id_as="$3" __dir="$4" __hook="$5"

ZINIT_ICE[.monitor]=${ZINIT_ICE[.monitor]:-${ZINIT_ICE[as]}}

if [[ ${ZINIT_ICE[as]} = ((monitor\||)(command|program)|(command|program)(\|monitor|)) ]] {
    ZINIT_ICE[as]=command
} elif [[ ${ZINIT_ICE[as]} = ((monitor\||)completion|completion(\|monitor|)) ]] {
    ZINIT_ICE[as]=completion
} elif [[ ${ZINIT_ICE[as]} = ((monitor\||)null|null(\|monitor|)) ]] {
    ZINIT_ICE[as]=null
    ZINIT_ICE[pick]=${ZINIT_ICE[pick]:-/dev/null}
} else {
    if [[ ${ZINIT_ICE[as]} != monitor ]] {
        builtin print -P "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-msg2]}" \
                "Warning: Unrecognized as'monitor' variant" \
                "(${ZINIT[col-obj]}${ZINIT_ICE[as]}${ZINIT[col-msg2]})." \
                "Falling back to ${ZINIT[col-obj]}as'monitor'${ZINIT[col-msg2]}.%f%b"
    }
    unset 'ZINIT_ICE[as]'
}

if [[ -d $__dir && -z ${opts[(r)-u]} ]] {
    return 0
}

local -a match mbegin mend reply
local MATCH REPLY
integer MBEGIN MEND

(( ${+functions[.zinit-setup-plugin-dir]} )) || builtin source ${ZINIT[BIN_DIR]}/zinit-install.zsh

match=()
local dlpage=${__url%(#b)([^+])++*}
dlpage=$dlpage${match[1]}

if [[ -z ${ZINIT_ICE[dlink]} ]] {
    local plus=${(MS)__url%%[^+]++##} pattern_url=$dlpage
    plus=${plus#?++}
    while [[ -n $plus ]] {
        pattern_url=${pattern_url:h}
        plus=${plus%+}
    }
    local pattern_url=${pattern_url}/${__url##*++}
} else {
    local -a urls
    urls=( ${(s.;.)ZINIT_ICE[dlink]} )
    if (( ${#urls} == 2 )) {
        local pattern_url=${urls[@]:#0*} pattern_url0=${${(M)urls[@]:#0*}##0(:|=)(#c0,1)}
    } elif (( ${#urls} > 2 )) {
        builtin print -P -- "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-error]}ERROR:" \
            "${ZINIT[col-msg2]}The ice ${ZINIT[col-obj2]}dlink''${ZINIT[col-msg2]}" \
            "has been used too many times (i.e.:" \
            "${ZINIT[col-obj]}${#urls}${ZINIT[col-msg2]}, while it can be used at most" \
            "two times, as ${ZINIT[col-obj]}dlink0''${ZINIT[col-msg2]}" \
            "and then ${ZINIT[col-obj]}dlink''${ZINIT[col-msg2]}), aborting.%f"
        return 9
    } else {
        local pattern_url=${ZINIT_ICE[dlink]}
    }
}

local tmpfile="$(mktemp)"

pattern_url=${pattern_url//\%VERSION\%/[.,a-zA-Z0-9_-]+}
local pattern_url0=${pattern_url0//\%VERSION\%/[.,a-zA-Z0-9_-]+}

local cur_paturl
for cur_paturl ( $pattern_url0 $pattern_url ) {
    .zinit-download-file-stdout $dlpage >! $tmpfile || {
        .zinit-download-file-stdout $dlpage 1 >! $tmpfile || {
            builtin print -P "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-error]}ERROR: couldn't" \
                "fetch the download page (${ZINIT[col-obj]}${dlpage//\%/%%}${ZINIT[col-error]})%f"
            return 9
        }
    }

    local -a list
    list=( ${(@f)"$(noglob command egrep -io "href=.$cur_paturl" $tmpfile)"} )

    local selected=${list[1]#href=?}

    if [[ -z $selected ]] {
        builtin print -P -- "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-error]}ERROR:%f" \
            "couldn't match the URL${${(M)cur_paturl:#$pattern_url0}:+-0} at the download page" \
            "(which is ${ZINIT[col-obj]}${dlpage//\%/%%}%f"\
"${${__id_as:#$__url}:+\; the snippet is being identified as" \
            "${ZINIT[col-obj2]}$__id_as%f%b}; was matching" \
            "${ZINIT[col-obj2]}$cur_paturl%f%b)."
        return 9
    }

    if [[ $selected = /* ]] {
        local domain protocol
        if [[ $dlpage = (#b)(#i)((http(s|)|ftp(s|)|ssh|scp)://|)([^/]##)(*) ]] {
            protocol=${match[1]} domain=${match[5]}
        }
        if [[ -z $domain ]] {
            builtin print -P -- "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-error]}ERROR:" \
                "couldn't establish the domain name" \
                "(unsupported protocol? supported are: http(s),ftp(s),ssh,scp).%f"
            return 9
        }
        local new_url=${protocol:-http://}$domain$selected
    } elif [[ $selected = (#i)(http(s|)|ftp(s|)|ssh|scp)://* ]] {
        local new_url=$selected
    } else {
        local -a exts
        exts=(
            xhtml htmls html htm php php3 php4 phtml
            pl asp aspx ece js jsp jspx jhtml cfm py
            rb rhtml shtml cgi
        )
        if [[ $dlpage = *.(${(~j:|:)exts}) ]] {
            local new_url=${dlpage:h}/$selected
        } else {
            local new_url=$dlpage/$selected
        }
    }

    # Assign in case there's dlink0 given (i.e.: that this
    # URL was the dlink0 URL)
    dlpage=$new_url
}

# Handle the update
if [[ -n ${opts[(r)-u]} && -z ${opts[(r)-f]} ]] {
    { local old_url="$(<$__dir/._zinit/url_rsvd)"; } 2>/dev/null
    [[ $old_url == $new_url ]] && return 8
}

# Assign outer-scope parameters
url=$new_url
ZINIT_ICE[url_rsvd]=$new_url
ICE_OPTS[opt_-q,--quiet]=0
ZINIT[annex-multi-flag:pull-active]=2

builtin print -P -- "${ZINIT[col-pre]}as-monitor annex: ${ZINIT[col-msg]}Matched the" \
    "following URL: ${ZINIT[col-obj]}$new_url%f"

return 0

# vim:ft=zsh:tw=80:sw=4:sts=4:et
